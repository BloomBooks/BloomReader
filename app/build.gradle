apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'
// This is the master definition of what version of Bloom Reader we are building
def versionMajor = 1
def versionMinor = 2
def versionRelease = getVersionRelease()

int getVersionRelease() {
    def fullVersionNumberFromBuildSystem = project.ext.properties['build.number'] ?: "0" // default for non-tc
    List tokens = fullVersionNumberFromBuildSystem.tokenize('.')
    return Integer.parseInt(tokens.last())
}

// Load our credentials
def fileName = "${System.properties['user.home']}${File.separator}keystore${File.separator}keystore_bloom_reader.properties"
def keystoreProperties
if (rootProject.file(fileName).exists()) {
    def keystorePropertiesFile = rootProject.file(fileName)
    keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 26
    buildToolsVersion '27.0.3'

    playAccountConfigs {
        defaultAccountConfig {
            if (keystoreProperties) {
                jsonFile = file(keystoreProperties['serviceAccountJsonFile'])
            }
        }
    }

    defaultConfig {
        applicationId "org.sil.bloom.reader"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode versionMajor * 100000 + versionMinor * 1000 + versionRelease.toInteger()
        versionName "${versionMajor}.${versionMinor}.${versionRelease}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary true
        buildConfigField "long", "TIMESTAMP", System.currentTimeMillis() + "L"
        playAccountConfig = playAccountConfigs.defaultAccountConfig
    }

    signingConfigs {
        release {
            if (keystoreProperties) {
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }

    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            minifyEnabled true
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }

    flavorDimensions "default" // To satisfy stupid new requirement in AS3.0.1

    productFlavors {
        alpha {
            applicationIdSuffix ".alpha"
            versionNameSuffix "-alpha"
            signingConfig signingConfigs.release
            dimension 'default'
        }
        beta {
            applicationIdSuffix ".beta"
            versionNameSuffix "-beta"
            signingConfig signingConfigs.release
            dimension 'default'
        }
        production {
            // no id or version name suffix; this is the main product.
            signingConfig signingConfigs.release
            dimension 'default'
        }
    }

    play {
        // This is definitely a hack, but I could find no other way to set the track based on what flavor we actually want to publish.
        // The problem is, if you try to do something like set a variable in the flavor and then set the track here using that variable,
        // the last time you set track is always the winner. And it always processes every flavor.
        // There is the potential someone could call with, e.g. publishAlphaRelease and publishBetaRelease, but I think I would
        // rather have an attempt to push a beta track on the alpha product (which would often fail because of version numbers, anyway)
        // than to have an attempt to push an alpha track on the beta or production products. Could probably be argued either way, though.
        if (project.gradle.startParameter.taskNames.any{tn -> tn.contains("ProductionRelease") || tn.contains("BetaRelease")})
            track = "beta"
        else
            track = "alpha"

        uploadImages = true
    }

    lintOptions {
        warning "MissingTranslation"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation  'com.android.support:support-v4:26.1.0'
    implementation  'com.android.support:recyclerview-v7:26.1.0'
    implementation  'com.android.support:design:26.1.0'
    // Supports the WiFi module's 'server' used to receive books from Bloom desktop
    implementation  'cz.msebera.android:httpclient:4.4.1.2'
    implementation  'com.segment.analytics.android:analytics:4.+'
    implementation  'commons-io:commons-io:2.4'
    // Andrew tried this and had problems. I don't recall the details.
    //implementation  'commons-io:commons-compress:1.15'
    // Needed (at least) for making tar archives to send multiple books.
    implementation  'org.apache.commons:commons-compress:1.14'
    implementation  'com.android.support.constraint:constraint-layout:1.1.3'

    testImplementation  'junit:junit:4.12'
    testImplementation  'org.mockito:mockito-core:2.22.0'
    testImplementation  'org.json:json:20180813'  // make org.json classes available in unit tests

    androidTestImplementation 'com.android.support:design:26.1.0'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:3.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-web:3.0.1'
}
