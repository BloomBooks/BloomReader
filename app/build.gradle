apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'
// This is the master definition of what version of Bloom Reader we are building
def versionMajor = 1
def versionMinor = 1
def versionRelease = project.ext.properties['build.number'] ?: 0 // default for non-tc

// Load our credentials
def fileName = "${System.properties['user.home']}${File.separator}keystore${File.separator}keystore_bloom_reader.properties"
def keystoreProperties
if (rootProject.file(fileName).exists()) {
    def keystorePropertiesFile = rootProject.file(fileName)
    keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 23
    buildToolsVersion '26.0.2'

    playAccountConfigs {
        defaultAccountConfig {
            if (keystoreProperties) {
                jsonFile = file(keystoreProperties['serviceAccountJsonFile'])
            }
        }
    }

    defaultConfig {
        applicationId "org.sil.bloom.reader"
        minSdkVersion 19
        targetSdkVersion 22
        versionCode versionMajor * 100000 + versionMinor * 1000 + versionRelease.toInteger()
        versionName "${versionMajor}.${versionMinor}.${versionRelease}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary true
        buildConfigField "long", "TIMESTAMP", System.currentTimeMillis() + "L"
        playAccountConfig = playAccountConfigs.defaultAccountConfig
    }

    signingConfigs {
        release {
            if (keystoreProperties) {
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }

    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            minifyEnabled true
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }

    flavorDimensions "default" // To satisfy stupid new requirement in AS3.0.1

    productFlavors {
        alpha {
            applicationIdSuffix ".alpha"
            versionNameSuffix "-alpha"
            signingConfig signingConfigs.release
            dimension 'default'
        }
        beta {
            applicationIdSuffix ".beta"
            versionNameSuffix "-beta"
            signingConfig signingConfigs.release
            dimension 'default'
        }
        production {
            // no id or version name suffix; this is the main product.
            signingConfig signingConfigs.release
            dimension 'default'
        }
    }

    play {
        // This is definitely a hack, but I could find no other way to set the track based on what flavor we actually want to publish.
        // The problem is, if you try to do something like set a variable in the flavor and then set the track here using that variable,
        // the last time you set track is always the winner. And it always processes every flavor.
        // There is the potential someone could call with, e.g. publishAlphaRelease and publishBetaRelease, but I think I would
        // rather have an attempt to push a beta track on the alpha product (which would often fail because of version numbers, anyway)
        // than to have an attempt to push an alpha track on the beta or production products. Could probably be argued either way, though.
        if (project.gradle.startParameter.taskNames.any{tn -> tn.contains("ProductionRelease") || tn.contains("BetaRelease")})
            track = "beta"
        else
            track = "alpha"

        uploadImages = true
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:support-v4:23.4.0'
    compile 'com.android.support:recyclerview-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    // Supports the WiFi module's 'server' used to receive books from Bloom desktop
    compile 'cz.msebera.android:httpclient:4.4.1.2'
    compile 'com.segment.analytics.android:analytics:4.+'
    compile 'commons-io:commons-io:2.4'
    // Andrew tried this and had problems. I don't recall the details.
    //compile 'commons-io:commons-compress:1.15'
    // Needed (at least) for making tar archives to send multiple books.
    compile 'org.apache.commons:commons-compress:1.14'

    testCompile 'junit:junit:4.12'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
}
